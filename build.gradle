buildscript {
    ext {
        kotlinVersion = '1.3.61'
        springBootVersion = '2.2.4.RELEASE'
        jetPluginVersion = '1.3.3'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlinVersion"
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "com.excelsiorjet:excelsior-jet-gradle-plugin:$jetPluginVersion"
    }
}

plugins {
    id "com.moowork.node" version "1.3.1"
    id 'com.palantir.docker' version '0.24.0'
    id 'com.google.cloud.tools.jib' version '1.8.0'
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: "kotlin-spring"
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'excelsiorJet'
apply from: 'build-js.gradle'

group 'amu'
version '2.2.3-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

springBoot  {
    buildInfo()
}

configurations {
    compile.exclude module: "spring-boot-starter-tomcat"
}

excelsiorJet {
    appType = "spring-boot"
}

task unpack(type: Copy) {
    dependsOn bootJar
    from(zipTree(tasks.bootJar.outputs.files.singleFile))
    into("build/dependency")
}

docker {
    name "zhcet/zhcet-server"
    tags "${project.version}"
    copySpec.from(tasks.unpack.outputs).into("dependency")
    buildArgs(['DEPENDENCY': "dependency"])
}

dockerPush.dependsOn(dockerTag)

dependencies {
    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("org.springframework.boot:spring-boot-starter-thymeleaf")
    implementation("org.springframework.boot:spring-boot-starter-data-jpa")
    implementation("org.springframework.boot:spring-boot-starter-security")
    implementation("org.springframework.boot:spring-boot-starter-mail")
    implementation("org.springframework.boot:spring-boot-starter-undertow")
    implementation("org.springframework.boot:spring-boot-devtools")
    implementation("org.springframework.boot:spring-boot-starter-actuator")
    compileOnly("org.springframework.boot:spring-boot-configuration-processor")
    implementation("org.thymeleaf.extras:thymeleaf-extras-springsecurity5")
    implementation 'javax.interceptor:javax.interceptor-api:1.2.2' // TODO: Remove when workaround fix is found
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    implementation 'org.postgresql:postgresql'
    implementation "org.hibernate:hibernate-envers"
    implementation "org.liquibase:liquibase-core:3.8.5"
    implementation "com.j256.simplecsv:simplecsv:2.6"
    implementation "org.apache.commons:commons-text:1.8"
    implementation 'commons-validator:commons-validator:1.6', {
        exclude group: 'commons-logging', module: 'commons-logging'
    }
    implementation 'org.modelmapper:modelmapper:2.3.6'
    implementation 'com.github.darrachequesne:spring-data-jpa-datatables:5.0.0'
    implementation 'com.google.firebase:firebase-admin:6.12.0'
    implementation 'commons-io:commons-io:2.6'
    implementation 'org.imgscalr:imgscalr-lib:4.2'
    implementation 'com.github.ben-manes.caffeine:caffeine:2.8.0'
    implementation 'org.jboss.aerogear:aerogear-otp-java:1.0.0'

    // Sentry
    implementation 'io.sentry:sentry-spring:1.7.29'
    implementation 'io.sentry:sentry-logback:1.7.29'

    // WebJars
    implementation 'org.webjars:toastr:2.1.2'
    implementation 'org.webjars.bower:mui:0.9.38'
    implementation 'org.webjars.npm:corejs-typeahead:1.2.1'

    implementation 'org.webjars:webjars-locator-core'

    compileOnly "org.projectlombok:lombok:1.18.10"

    // Workaround for Java 9+ modules missing problem
    implementation 'javax.xml.bind:jaxb-api'
    implementation 'com.sun.xml.bind:jaxb-impl:2.3.2'
    implementation 'com.sun.xml.bind:jaxb-core:2.3.0.1'

    testImplementation group: 'junit', name: 'junit', version: '4.13'
    testImplementation("org.springframework.boot:spring-boot-starter-test")
    testImplementation("org.springframework.security:spring-security-test")
}

compileJava.dependsOn(processResources)
