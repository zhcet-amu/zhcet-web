buildscript {
    ext {
        springBootVersion = '1.5.4.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id 'net.ltgt.apt' version '0.8'
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'

group 'in.ac.amu'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

// attach new global property to the `project`. Will not override by default
def safeLoad(String name, Object value, Boolean override = false) {
    if (!hasProperty(name) || override)
        project.ext.set(name as String, value as String)
}

File secretPropsFile = file('secret.properties')
if (secretPropsFile.exists()) {
    println 'Loading Secret Properties...'
    Properties p = new Properties()
    p.load(new FileInputStream(secretPropsFile))
    p.each { name, value ->
        safeLoad name as String, value
    }
} else {
    println 'Secret Properties not found. Relying on Environment Variables'
}

processResources {
    // Saving secrets in application.properties
    if (secretPropsFile.exists()) {
        filesMatching("**/application.properties") {
            expand(project.ext.properties)
        }
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile("org.springframework.boot:spring-boot-starter-thymeleaf")
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("org.springframework.boot:spring-boot-starter-security")
    compile("org.springframework.boot:spring-boot-devtools")
    compile("org.springframework.boot:spring-boot-starter-mail")
    // Force Thymeleaf 3 for full HTML support
    compile("org.thymeleaf:thymeleaf:3.0.6.RELEASE")
    compile("org.thymeleaf:thymeleaf-spring4:3.0.6.RELEASE")
    compile("org.thymeleaf.extras:thymeleaf-extras-springsecurity4:3.0.2.RELEASE")
    compile group: 'org.thymeleaf.extras', name: 'thymeleaf-extras-java8time', version: '3.0.1.RELEASE'
    compile("nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect:2.2.2")
    compile group: 'org.hibernate', name: 'hibernate-core', version: '5.2.10.Final'
    compile group: 'org.hibernate', name: 'hibernate-entitymanager', version: '5.2.10.Final'
    compile "com.j256.simplecsv:simplecsv:2.1"
    compile "org.flywaydb:flyway-core:4.2.0"
    compile "org.apache.commons:commons-text:1.1"
    compile 'commons-validator:commons-validator:1.4.1'
    compile 'org.modelmapper:modelmapper:1.1.0'
    compile 'mysql:mysql-connector-java'
    compile 'com.github.darrachequesne:spring-data-jpa-datatables:4.1'
    compile 'com.google.firebase:firebase-admin:5.3.0'
    compile group: 'commons-io', name: 'commons-io', version: '2.5'
    compile group: 'org.imgscalr', name: 'imgscalr-lib', version: '4.2'
    compile 'org.mapstruct:mapstruct-jdk8:1.1.0.Final'
    compile 'io.sentry:sentry-spring:1.5.4'
    compile 'io.sentry:sentry-logback:1.5.4'
    compileOnly "org.projectlombok:lombok:1.16.18"
    compileOnly 'org.mapstruct:mapstruct-processor:1.1.0.Final'
}
